ThisPlace
{
	if thisPlaceLvl > 0 //&& thisPlaceClosed==0
	{	
		if thisPlacePaused == 1
		{
			"%(g)\![*]\q[This place,OnThisPlaceTalk]%(w)"			
		}
		else
		{
			"\![*]\q[This place,OnThisPlaceTalk]"			
		}
		
	}
}

ThisPlaceLvlUp  
{	
	exp = thisPlaceExp; lvl = thisPlaceLvl; paused = thisPlacePaused;			
	closed = thisPlaceClosed;
	
	ShouldLvlUp
		
	thisPlaceExp = exp; thisPlaceLvl = lvl; thisPlacePaused = paused;
	exp = 0; lvl = 0; paused = 0; closed = 0;	

}

ThisPlaceUpsetAfterFriday
{
	"\s[0]\i[2]\i[108]\i[109]%(z)I did say this place is like a limbo...%(d)\%(wx)\%(wx)\s[0]\i[2]\n\n * (They shudder.)\%(wx)\s[0]"
	--
	thisPlacePaused = 1
	//NumOfOpenSubjects--	
	thisPlaceExp = 0
	upsetAfterFriday++
}

OnThisPlaceTalk
{	
	_rnd = RAND(100)
	if _rnd < 5
	{
		thisPlaceExp++
	}
	--
	if thisPlacePaused == 0  &&  TOINT(upsetAfterFriday)==0
	{
		case thisPlaceLvl
		{
			when 1
			{
				dreamsExp ++
				--
				"\0\s[0]* (You ask Zilch what they think of this place.)\%(wx)\n\n[half]%(z)\i[115]\i[114]It's a very strange dream!\%(wx)%(d)\s[0]"
			}
			when 2
			{
				"\0\s[0]* (You wonder if you should tell them why they're here.)\n\n[half]\![*]\q[Naaahh,notExplain]\n\![*]\q[Tell them the truth,TellTruth]\n\![*]\q[Make something up,MakeSomethingUp]\n"				
			}
			when 3
			{
				"\0\s[0]* (You say that in this place, you can't always tell them exactly what you want to say.)\%(wx)\n\n%(z)\s[0]\i[102]\i[103]I know what that's like!\%(wx) \s[0]\i[109]Sometimes I can't explain myself at all%(d).\%(wx)"
				--
				if familiarity > 50 && upset==0
				{
					"\0\s[0]\i[103]%(z)...but you're doing all right!%(d)"
				}
			}							
		}
		--
		if thisPlaceLvl > 3
		{
			ThisPlacePool
		}		
		else
		{
			thisPlacePaused = 1	
			//NumOfOpenSubjects--
		}
		
	}
	elseif TOINT(upsetAfterFriday)!=0
	{
		ThisPlaceUpsetAfterFriday
	}
	else
	{
		"\0\s[0]\i[103]\i[102]%(z)I don't know anything else about this place...%(d)\%(wx)\0\s[0]"
	}
	--
	"\e"
	
}


Select.TellTruth
{
	"\0\s[0]* (You tell Zilch they're a ghost inside your computer)\%(wx)\%(wx)\c%(z)Ghosts aren't real\i[115], and even if they were...\%(wx) \s[0]\i[117]\i[103]Why would dead people want to haunt your computer?\%(wx) \s[0]\i[105]\i[102]Maybe watch less horror films?\%(wx)%(d)\s[0]"			
}

Select.MakeSomethingUp
{
	"\0\s[0]* (You tell Zilch that you have opened a portal into their dreams through SCIENCE!)\%(wx)\n\n\s[0]\i[110]\i[109]%(z)Cool!\%(wx) \s[0]I'm a scientist too.\%(wx)\s[0]\i[102]\i[105] So I want you to give me a proof!%(d)\%(wx)\s[0]\n"
	--
	waitingForProof = 1
	scienceExp++	
	themselfExp++
	youLiar++
}

Select.notExplain
{
	"\0\s[0]* (You decide not to tell them.\%(wx) You dance a goofy dance instead.\%(wx)\s[0]\i[110]\i[109] It's very expressive.)\w8\n\n\c%(z)Heehee!\s[0]\i[113]\i[114]\i[53]\w6\s[0]\i[113]\i[114]\i[52]\w6\s[0]\i[113]\i[114]\i[53]\w6\s[0]\%(wx)\c%(d)* (It's better this way.)\n\%(wx)"
	--	
	artExp ++
	harmonicaExp ++
	familiarity ++
}

ThisPlaceMaxLinesInPool
{
	"11"
}

ThisPlacePool
{
	if chooseThisPlace >=TOINT(ThisPlaceMaxLinesInPool) //match to total number of lines in pool
	{
		chooseThisPlace = TOINT(0)
	}
	--
	chooseThisPlace++
	--		
	case chooseThisPlace
	{
		when 1
		{
			"\0\s[0]\i[111]%(z)There's a lot of dust here. \s[0]\i[105]\i[102]When did you last clean this place, %(username)?%(d)\%(wx)\s[0]\e"
			
		}
		when 2
		{
			artExp++
			--
			"\0\s[0]\i[118]%(z)Is it okay if I look at the pictures in your folder?%(d)\%(wx)\s[0]\n\n[half]\![*]\q[Yes,nothing]\n\![*]\q[Get out of my folder Zilch!!!,nothing]\n"			
		}
		when 3
		{
			scienceExp++
			--		
			"\0\s[0]\i[103]%(z)You know what I found out?\%(wx) \s[0]\i[115]\i[114]I mostly end up here at %(OnFindMostVisitedHour)!\s[0] More research is required!%(d)\%(wx)"			
		}
		when 4
		{
			"\0\s[0]\i[109]%(z)Ew there's a worm here.%(d)\%(wx)\s[0]\e"		
		}
		when 5
		{
			scienceExp++
			dreamsExp++
			--
			"\0\s[0]\i[102]%(z)This place is like a limbo between the real world and the dream world.\%(wx) \s[0]\i[111]I need to research it!%(d)\%(wx)\s[0]"			
		}
		when 6
		{
			"\0\s[0]%(z)\i[102]Is there anything to eat in this place?%(d)\%(wx)\s[0]\n\n%(FoodHere)"			
		}
		when 7
		{
			artExp++
			--
			"\0\s[0]%(z)\i[103]How are things on your side of the screen?\%(wx) What's your wallpaper like?%(d)\%(wx)\s[0]"			
		}
		when 8
		{
			"\0\s[0]\i[108]\i[109]%(z)*sneeze* \%(wx)\s[0]...I had a really bad cold.\%(wx)%(d)\n\n* (Your computer is now infected with Zilch's virus.)\%(wx)\s[0]"
		}
		when 9
		{
			"\0\s[0]\i[111]%(z)can you open a window to anywhere you like?%(d)\%(wx) \n\n\![*]\q[Pretty much,YesWindow].\%(wx)"  			
		}
		when 10
		{
			if wanthome == 2 && passhour >=1
			{
				"* (Zilch sulks).\%(wx)\n\n\0\s[0]\i[117]\i[2]%(z)I'm tired!\%(wx) Can I leave?\%(wx)%(d)\n\n\![*]\q[Sure,Leave]\n\![*]\q[Nope,NotLeave]\n"
			}
			else			
			{
				ThisPlacePool
			}
		}	
		when 11
		{
			"%(z)\0\s[0]I wonder what's on the other side of the screen...%(d)\%(wx)\![move,0,0,3000]\%(wx)\0\s[-1]\![move,1200,0,0]\c\0\s[0]\i[111]\i[110]%(z)It wraps around!\%(wx)%(d)\s[0]"
		}
	}
}


Select.Leave   
{
	LessUpsetNow
	--
	"\0\s[0]\i[117]\i[103]%(z)Byeeeeee!%(d)\%(wx)\-\e"
}

Select.NotLeave
{
	"\0\s[0]\i[113]\i[114]\i[5]%(z)But I'm tired!%(d)\%(wx)\n\n\![*]\q[Tough luck,toughluck]\n\![*]\q[I'll build you a bed,Buildbed]\n"		
}

Select.toughluck
{
	"\0\s[0]"
	--
	upset+=5
	latestSin = "KeptTired"
	AddSinToList	
}

Select.Buildbed
{
	LessUpsetNow
	--
	"%(z)\0\s[0]\i[108]\i[109]Ooooh! How do you do that?%(d)\%(wx)\n\n\![*]\q[Press the button and...,PressButton]\n"
}

Select.PressButton
{
	"\s[0]%(z)That doesn't sound like the right way to do it!%(d)\%(wx)\n\n* (Nothing happens.)\%(wx)"
}


Select.YesWindow
{
	"%(z)\s[0]\i[113]\i[114]Ooooh,\%(wx) I wish I could do that!\%(wx) \s[0]\i[105]\i[115]Then I could just open a window to the northern pole\s[0]\i[102]\i[103] and use it as an ice-cream refrigerator!%(d)\%(wx)\s[0]"	
}

FoodHere
{	
	"\![*]\q[No,NoFood]\n\![*]\q[Only a byte ;),Byte]\n"		
}


Select.NoFood
{
	"\0\s[0]\i[2]%(z)Disappointment,\%(wx) your name is %(username)!%(d)\%(wx)\s[0]"	
}

Select.Byte
{
	"\0\s[0]%(z)%(excited)%(excited)\s[0]\i[113]\i[114]I want a bite!\%(wx)\s[0]%(d)\%(wx)\n\n* (You can now feed Zilch with candy-bytes.\%(wx) They're worthless,\%(wx) nutrition-wise.\%(wx) Just like actual candies.)\%(wx)\n\n%(z)\s[0]\i[113]\i[114]\i[55]\i[54]But they're yummy!\%(wx)\s[0]%(d)"	
	--
	canHaveCandy=1
}